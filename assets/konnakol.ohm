Konnakol {
  Composition = Tala? TempoChunk+

  // TODO: temp!
  // stage 1: list of common talas
  // stage 2: separate grammar for constructing with Anga notation IOII etc...
  Tala = ("adi" | "rupaka")? ("tala" | "talam")

  TempoChunk = Tempo? Chunk+

  Tempo = ("tempo"|"kala") digit+

  Gati = "g" digit+

  Repeat = ("*" | "x") digit+

  Chunk = Gati? (ChunkDouble | ChunkHalf | (~Tempo word))+ Repeat? --normal
     | Gati? "(" Chunk+ ")" Repeat? --paren

  ChunkDouble =
       "{" ChunkDouble "}" --recur
     | "{" Chunk+ "}" -- base

  ChunkHalf =
       "[" ChunkHalf "]" --recur
     | "[" Chunk+ "]" --base

  word = syllable+

  syllable =
      consonant      vowel  --normal
    | upperConsonant vowel  --stressed
    | silence

  silence =
      "," --extension
    | "-" --rest

  consonant = &("a".."z") ~vowel letter consonant? // or ("h"|"r"|"l"|"y"|"w")?

  upperConsonant = &("A".."Z") ~upperVowel letter "h"?

  modVowel = consonant+ ~vowel

  vowel = vowel modVowel --mod
      | "a"+        --a
      | "e"+        --e
      | "i"+        --i
      | "o"+        --o
      | "u"+        --u

  // upperVowel is *not* used by the semantics, only for negating in upperConsonant
  upperVowel = ("A"|"E"|"I"|"O"|"U")
}
